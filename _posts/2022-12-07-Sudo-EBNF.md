---
title: 使用 sudo
date: 2022-12-07
modify_date: 2022-12-07
tags: Devops
key: Sudo-EBNF-2022-12-07
---

## sudo 语法

```text
USERS HOSTS=(RUNAS) [NOPASSWD:]COMMANDS
```

例如，允许用户 peter 以 root 用户身份在 hostA 主机上执行 `/usr/bin/dnf` 命令

```sh
peter hostA=(root) /usr/bin/dnf
```

查看 `man sudoers` 的 `EXAMPLE` 章节，学习更多例子。

<!--more-->

### 为特定用户配置 sudo

使用 `sudo visudo` 命令会直接编辑系统默认配置，对于普通用户，更好的方法是在 `/etc/sudoers.d` 目录下单独添加配置文件，文件名可以使用用户名

```sh
sudo visudo /etc/sudoers.d/$USER
```

#### /etc/sudoers 配置文件

##### sudoers options

```conf
# 不再需要用户输入密码
Defaults:username      !authenticate

# 输入密码后 30 分钟内不需要再次输入密码
Defaults               timestamp_timeout=30
```

##### User specifications

```conf
# 让用户可以无密码运行任何命令
username ALL=(ALL) NOPASSWD:ALL

# 所有属于 wheel 用户组的用户可以无密码运行任何命令
%wheel ALL=(ALL) NOPASSWD:ALL
```

## 使用 sudo

切换到不同用户

```sh
# root user
sudo -i

# foo user
sudo -i -u foo
```

以不同用户执行命令

```sh
# run dnf update command as root user
sudo dnf update

# run ls -l command as foo user
sudo -u foo ls -l
```

## sudo EBNF

使用 [Railroad Diagram Generator](https://bottlecaps.de/rr/ui) 加载以下 sudo EBNF 生成轨道图

> The User specification is the part that actually determines who may run what.

```bnf
Alias ::= 'User_Alias'  User_Alias (':' User_Alias)* |
          'Runas_Alias' Runas_Alias (':' Runas_Alias)* |
          'Host_Alias'  Host_Alias (':' Host_Alias)* |
          'Cmnd_Alias'  Cmnd_Alias (':' Cmnd_Alias)*

User_Alias ::= NAME '=' User_List

Runas_Alias ::= NAME '=' Runas_List

Host_Alias ::= NAME '=' Host_List

Cmnd_Alias ::= NAME '=' Cmnd_List

NAME ::= [A-Z]([A-Z][0-9]_)*

User_List ::= User |
              User ',' User_List

User ::= '!'* user_name |
         '!'* '#' uid |
         '!'* '%' group |
         '!'* '%#' gid |
         '!'* '+' netgroup |
         '!'* '%:' nonunix_group |
         '!'* '%:#' nonunix_gid |
         '!'* User_Alias

Runas_List ::= Runas_Member |
               Runas_Member ',' Runas_List

Runas_Member ::= '!'* user_name |
                 '!'* '#' uid |
                 '!'* '%' group |
                 '!'* '%#' gid |
                 '!'* '%:' nonunix_group |
                 '!'* '%:#' nonunix_gid |
                 '!'* '+' netgroup |
                 '!'* Runas_Alias

Host_List ::= Host |
              Host ',' Host_List

Host ::= '!'* host_name |
         '!'* ip_addr |
         '!'* network('/'netmask)? |
         '!'* '+' netgroup |
         '!'* Host_Alias

Cmnd_List ::= Cmnd |
              Cmnd ',' Cmnd_List

command_name ::= file_name |
                 file_name args |
                 file_name '""'

Cmnd ::= '!'* command_name |
         '!'* directory |
         '!'* "sudoedit" |
         '!'* Cmnd_Alias

Default_Type ::= 'Defaults' |
                 'Defaults' '@' Host_List |
                 'Defaults' ':' User_List |
                 'Defaults' '!' Cmnd_List |
                 'Defaults' '>' Runas_List

Default_Entry ::= Default_Type Parameter_List

Parameter_List ::= Parameter |
                   Parameter ',' Parameter_List

Parameter ::= Parameter '=' Value |
              Parameter '+=' Value |
              Parameter '-=' Value |
              '!'* Parameter

User_Spec ::= User_List Host_List '=' Cmnd_Spec_List 
              (':' Host_List '=' Cmnd_Spec_List)*

Cmnd_Spec_List ::= Cmnd_Spec |
                   Cmnd_Spec ',' Cmnd_Spec_List

Cmnd_Spec ::= Runas_Spec? SELinux_Spec? Tag_Spec* Cmnd

Runas_Spec ::= '(' Runas_List? (':' Runas_List)? ')'

SELinux_Spec ::= ('ROLE=role' | 'TYPE=type')

Tag_Spec ::= ('NOPASSWD:' | 'PASSWD:' | 'NOEXEC:' | 'EXEC:' |
              'SETENV:' | 'NOSETENV:' | 'LOG_INPUT:' | 'NOLOG_INPUT:' |
              'LOG_OUTPUT:' | 'NOLOG_OUTPUT:')
```

## 参考文档

- [BNF diagram for sudo file](https://serverfault.com/questions/696085/bnf-diagram-for-sudo-file)
- [Sudoers Manual](https://www.sudo.ws/docs/man/1.7.10/sudoers.man/)
